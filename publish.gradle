apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    afterEvaluate {
        tasks.named('sourceReleaseJar') {
            archiveFileName.set('sources.jar')
        }
        tasks.named('javaDocReleaseJar') {
            archiveFileName.set('javadoc.jar')
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }
            pom {
                name.set('Support')
                description.set('This project is a suite of libraries for Android.')
                url.set('https://gitlab.com/kfaraj/support')
                licenses {
                    license {
                        name.set('Apache License, Version 2.0')
                        url.set('https://www.apache.org/licenses/LICENSE-2.0')
                    }
                }
                developers {
                    developer {
                        name.set('Kamal Faraj')
                        email.set('kfaraj.dev@gmail.com')
                    }
                }
                scm {
                    connection.set('scm:git:https://gitlab.com/kfaraj/support.git')
                    developerConnection.set('scm:git:https://gitlab.com/kfaraj/support.git')
                    url.set('https://gitlab.com/kfaraj/support')
                }
            }
        }
    }
    repositories {
        maven {
            def releaseUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('-SNAPSHOT') ? snapshotUrl : releaseUrl
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.release
}
