apply plugin: 'maven-publish'
apply plugin: 'signing'

android {
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            afterEvaluate {
                from components.release
            }
            pom {
                name.set(pomName)
                description.set(pomDescription)
                url.set(pomUrl)
                licenses {
                    license {
                        name.set('Apache-2.0')
                        url.set('https://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }
                developers {
                    developer {
                        name.set('Kamal Faraj')
                        email.set('kfaraj.dev@gmail.com')
                    }
                }
                scm {
                    connection.set('scm:git:https://gitlab.com/kfaraj/support.git')
                    developerConnection.set('scm:git:https://gitlab.com/kfaraj/support.git')
                    url.set('https://gitlab.com/kfaraj/support')
                }
            }
        }
    }
    repositories {
        maven {
            def stagingUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('-SNAPSHOT') ? snapshotsUrl : stagingUrl
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
        }
    }
}

signing {
    sign publishing.publications.release
}
