apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

task generateJavadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task generateJavadocJar(type: Jar, dependsOn: generateJavadoc) {
    archiveClassifier.set('javadoc')
    from generateJavadoc.destinationDir
}

task generateSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact generateJavadocJar
                artifact generateSourcesJar
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['release']
    pkg {
        repo = 'maven'
        name = project.group + '.' + project.name
        licenses = ['Apache-2.0']
        vcsUrl = 'https://gitlab.com/kfaraj/support.git'
        version {
            name = project.version
            vcsTag = project.name + "-v" + project.version
        }
    }
}
